#pragma kernel CSMain

// textures
RWTexture2D<float4> Texture;
Texture2D<float4> Source;

// matrixes
float4x4 CamToWorld;
float4x4 CamInverseProjection;

// variables
int Iterations;
float Scale;
bool Julia;
float3 C;
float Mix;
float3 Color;

// constants
static const int steps = 400;
static const float epsilon = 0.0001;

// structs
struct Ray { float3 origin; float3 direction; };
struct Data { float n; float3 p; };

// data contructor
Data DataConstr(float n, float3 p) {
	Data data;
	data.n = n;
	data.p = p;
	return data;
}

// sphere fold from http://blog.hvidtfeldts.net/index.php/2011/11/distance-estimated-3d-fractals-vi-the-mandelbox/
void SphereFold(inout float3 z, inout float dz) {
	float fixedRadius = 1, minRadius = 0.5;
	float fixedRadius2 = pow(fixedRadius, 2), minRadius2 = pow(minRadius, 2);
	float r2 = dot(z, z);
	
	if (fixedRadius < minRadius2) {
		float temp = (fixedRadius2 / minRadius2);
		z *= temp, dz *= temp;
	} else if (r2 < fixedRadius2) {
		float temp = (fixedRadius2 / r2);
		z *= temp, dz *= temp;
	}
}

// box fold from http://blog.hvidtfeldts.net/index.php/2011/11/distance-estimated-3d-fractals-vi-the-mandelbox/
void BoxFold(inout float3 z) {
	float foldingLimit = 1;
	z = clamp(z, -foldingLimit, foldingLimit) * 2.0 - z;
}

// orbit trap
void Trap(float3 z, inout float3 trap) {
	if (length(z) < length(trap))
		trap = z;
}

// mandelbox distance estimator from http://blog.hvidtfeldts.net/index.php/2011/11/distance-estimated-3d-fractals-vi-the-mandelbox/
Data DE(float3 p) {
	float3 z = p, trap = p;
	float dr = 1.0;
	
	for (int i = 0; i < Iterations; i++) {
		BoxFold(z); // box fold
		SphereFold(z, dr); // sphere fold
		
		z *= Scale; // scale
		z += Julia ? C : p; // c
		dr = dr * abs(Scale) + 1.0; // dr
		
		Trap(z, trap); // trap
	}
	
	return DataConstr(length(z) / abs(dr), trap);
}

Ray CreateRay(float3 origin, float3 direction) {
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    return ray;
}

Ray CreateCameraRay(float2 uv) {
    float3 origin = mul(CamToWorld, float4(0, 0, 0, 1)).xyz;
    float3 direction = mul(CamInverseProjection, float4(uv, 0, 1)).xyz;
    direction = mul(CamToWorld, float4(direction, 0)).xyz;
    direction = normalize(direction);
    return CreateRay(origin, direction);
}

// cast a ray and return the result
Data March(Ray ray) {
	Data d;
	float3 eye = ray.origin;
	
	float s = 0;
	while (s < steps) {
		d = DE(ray.origin); // calculate distance
		if (length(eye - ray.origin) > 100) s = steps; // to far away
		if (d.n < epsilon) break; // hit
		ray.origin += ray.direction * d.n; // march
		s++; // next iteration
	}
	
	return DataConstr(abs(1 - (s / steps)), d.p);
}

// main method
[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID) {
		
	// convert into range [-1, 1]
	float w, h; Texture.GetDimensions(w, h);
	float2 uv = id.xy / float2(w, h) * 2 - 1;
	
	// create ray
	Ray ray = CreateCameraRay(uv);
	
	// render
	Data res = March(ray); 
	
	float3 color = (Color / 255) * Mix + normalize(abs(res.p)) * (1 - Mix);
	Texture[id.xy] = Source[id.xy] + pow(res.n, 2) * float4(color, 1);
}