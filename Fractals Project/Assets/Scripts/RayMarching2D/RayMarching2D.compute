#pragma kernel CSMain

// textures
RWTexture2D<float4> Texture;
Texture2D<float4> Source;

// structs
struct Shape { float2 pos; float3 size; int type; int border; float3 color; };

// buffers
StructuredBuffer<Shape> Shapes;

// circle distance estimator
float DECircle(float2 c, float2 pos, float radius) {
	return length(c - pos) - radius;
}

// box distance estimator from https://iquilezles.org/www/articles/distfunctions2d/distfunctions2d.htm
float DEBox(float2 c, float2 pos, float2 size) {
	float2 d = abs(c - pos) - size;
	return length(max(d, float2(0, 0))) + min(max(d.x, d.y), 0);
}

// line distance estimator from https://iquilezles.org/www/articles/distfunctions2d/distfunctions2d.htm
float DELine(float2 c, float2 a, float3 size) {
	float2 b = float2(size[0], size[1]);
	float2 ca = c - a, ba = b - a;
	float h = clamp(dot(ca,ba)/dot(ba,ba), 0.0, 1.0);
	return length(ca - ba * h) - size[2];
}

// main method
[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID) {

	// add the background
	Texture[id.xy] = Source[id.xy];
	uint c, s; Shapes.GetDimensions(c, s);

	// loop through shapes
	for (int i = 0; i < (int) c; i++) {
		Shape shape = Shapes[i];
		float d = 0;
		if (shape.type == 0) {
			d = DECircle(id.xy, shape.pos, shape.size[0]);
		};
		if (shape.type == 1) {
			d = DEBox(id.xy, shape.pos, float2(shape.size[0], shape.size[1]));
		};
		if (shape.type == 2) {
			d = DELine(id.xy, shape.pos, shape.size);
		};
		if (shape.border == 0) {
			if (d <= 0) {
				Texture[id.xy] = float4(shape.color / 255, 0);
			};
		};
		if (shape.border == 1) {
			if (-1 <= d && d <= 1) {
				Texture[id.xy] = float4(shape.color / 255, 0);
			};
		};
	}
}
