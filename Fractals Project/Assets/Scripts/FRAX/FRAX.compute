#pragma kernel CSMain

// textures
RWTexture2D<float4> Texture;
Texture2D<float4> Source;

// matrixes
float4x4 CamToWorld;
float4x4 CamInverseProjection;

// variables
float zNear;
float zFar;
float Edge;
float3 CurveSpace;

// constants
static const int steps = 300;
static const float epsilon = 0.0001;
static const float size = 5;
static const int iterations = 5;

// structs
struct Ray { float3 origin; float3 direction; };

Ray CreateRay(float3 origin, float3 direction) {
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    return ray;
}

Ray CreateCameraRay(float2 uv) {
    float3 origin = mul(CamToWorld, float4(0, 0, 0, 1)).xyz;
    float3 direction = mul(CamInverseProjection, float4(uv, 0, 1)).xyz;
    direction = mul(CamToWorld, float4(direction, 0)).xyz;
    direction = normalize(direction);
    return CreateRay(origin, direction);
}

// box distance estimator from https://iquilezles.org/www/articles/distfunctions/distfunctions.htm
float DEBox(float3 p, float s) {
	float3 q = abs(p) - float3(s, s, s);
	return length(max(q, 0.0)) + min(max(q.x, max(q.y, q.z)), 0.0);
}

// 2D box distance estimator from https://iquilezles.org/www/articles/distfunctions2d/distfunctions2d.htm
float DEBox2D(float2 p, float s) {
	float2 d = abs(p) - s;
	return length(max(d, 0.0)) + min(max(d.x, d.y), 0);
}

// cross distance estimator from https://iquilezles.org/www/articles/distfunctions/distfunctions.htm
float DECross(float3 p, float s) {
	s = s / 3;
	float d = DEBox2D(p.xy, s);
	d = min(d, DEBox2D(p.zx, s));
	return min(d, DEBox2D(p.yz, s));
}

// repeat space from https://iquilezles.org/www/articles/distfunctions/distfunctions.htm
float3 RepeatSpace(float3 p) {
	float a = size / 3 * 4;
	float x = abs(p.x - a / 2) % a - a / 2;
	float y = abs(p.y - a / 2) % a - a / 2;
	float z = abs(p.z - a / 2) % a - a / 2;
	return float3(x, y, z);
}

float3 ClampPosition(float3 p) {
	float a = size / 3 * 4;
	float x = (p.x - a / 2) % a + a / 2;
	float y = (p.y - a / 2) % a + a / 2;
	float z = (p.z - a / 2) % a + a / 2;
	return float3(x, y, z);
}

// the distance to the scene
float DE(float3 p) {
	p = RepeatSpace(p);

	float d = DEBox(p, size / Edge);
	d = max(-DECross(p, size), d);
	
	// iterate and subtract
	int i = 0;
	while(i < iterations - 1) {
		p *= 3;
		p = abs(p);
		p -= float3(size, size, size);
		p = abs(p);
		p -= float3(size, size, size);
		i++;
		d = max(d, -DECross(p, size) / pow(3, i));
	}
	
	return d;
}

// cast a ray and return the result
float2 March(Ray ray) {
	float d = 0;
	float3 eye = ray.origin;
	
	int s = 0;
	while (s < steps) {
		d = DE(ray.origin); // calculate distance
		ray.direction += CurveSpace * d / 100; // curve space
		if (length(eye - ray.origin) > 500) break; // to far away
		if (d < epsilon) break; // hit
		ray.origin += ray.direction * d; // march
		s++; // next iteration
	}
	
	return float2(s / float(steps), length(eye - ray.origin));
}

float DecodeDepth(float d) {
	float a = zFar / (zFar - zNear);
	float b = zFar * zNear / (zNear - zFar);
	return b / (d - a);
}

// main method
[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID) {

	float4 accent = float4(0.55, 0.65, 1, 1);
	float4 collision = float4(0.55, 0.4, 0.8, 1);
	float4 background = float4(0.06, 0.06, 0.12, 0);
			
	// convert into range [-1, 1]
	float w, h; Texture.GetDimensions(w, h);
	float2 uv = id.xy / float2(w, h) * 2 - 1;
	
	// create ray
	Ray ray = CreateCameraRay(uv);
	
	ray.origin = ClampPosition(ray.origin);
	
	float2 res = March(ray);
	float4 ao = res.x * accent;
	if (DecodeDepth(Source[id.xy].w) > res.y)
		Texture[id.xy] = background + ao;
	else
		Texture[id.xy] = Source[id.xy];
	float a = DecodeDepth(Source[id.xy].w);
	float b = res.y;
	
	// collision
	if (abs(a - b) < 0.02) {
		Texture[id.xy] = background + collision;
	}
}
